// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'com.github.ben-manes.versions'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.3'
        classpath 'me.tatarka:gradle-retrolambda:3.3.0-beta4'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        // A modified version of lombok ast that allows lint to run on java 8 sources without error.
        classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
    // Exclude the version that the android plugin depends on.
    configurations.classpath.exclude group: 'com.android.tools.external.lombok'
}

allprojects {
    repositories {
        jcenter()
    }
}

subprojects { project ->
    afterEvaluate {
        project.apply from: "${project.rootDir}/gradle/findbugs_plugin.gradle"
        project.apply from: "${project.rootDir}/gradle/checkstyle_plugin.gradle"

        project.dependencies {
            testCompile 'junit:junit:4.12'
            testCompile 'org.mockito:mockito-all:1.10.19'
            testCompile "org.robolectric:robolectric:3.1.2"
            testCompile "org.powermock:powermock-module-junit4:1.6.5"
            testCompile "org.powermock:powermock-module-junit4-rule:1.6.5"
            testCompile "org.powermock:powermock-api-mockito:1.6.5"
            testCompile "org.powermock:powermock-classloading-xstream:1.6.5"

            androidTestCompile "com.android.support:support-annotations:${SUPPORT_LIBRARY_VERSION}"
            androidTestCompile 'com.android.support.test:runner:0.5'
            androidTestCompile 'com.android.support.test:rules:0.5'
            androidTestCompile ('com.android.support.test.espresso:espresso-core:2.2.2') {
                exclude module: 'support-annotations'
            }
            // Requires Android 4.3 (API level 18) or higher.
            androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
            androidTestCompile 'org.hamcrest:hamcrest-library:1.1'
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    BUILD_TOOLS_VERSION = '23.0.3'
    SUPPORT_LIBRARY_VERSION = '24.2.0'
}
